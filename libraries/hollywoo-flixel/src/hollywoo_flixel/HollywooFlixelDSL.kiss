(loadFrom "hollywoo" "src/hollywoo/HollywooDSL.kiss")

(defMacroVar subclass true)
(loadFrom "hollywoo" "src/hollywoo/Movie.kiss")
(loadFrom "hollywoo-flixel" "src/hollywoo_flixel/Aliases.kiss")

(method :Dynamic callPrivate [:Dynamic obj :String method &rest :Array<Dynamic> args]
    (Reflect.callMethod obj (Reflect.field obj method) args))

(method tween [:FlxTween tween]
    (callPrivate FlxTween.globalManager "remove" tween false)
    (tweens.push tween))
(method :TweenOptions tweenOpts [&opt :Void->Void cc]
    (object
        onComplete 
            ->:Void tween {
                            (tweens.remove tween)
                            (when cc (cc))
                        }))

(method linearMotion [:FlxSprite sprite :Float destX :Float destY :Float speed &opt :Void->Void cc :String soundLoop :Float volumeMod]
    (when soundLoop
        (let [oldCC cc]
            (set cc
                (makeCC
                    (stopSound soundLoop (makeCC null))
                    (when oldCC
                        (oldCC)))))
        (loopSound soundLoop (makeCC null) volumeMod))
    (tween
        (FlxTween.linearMotion sprite sprite.x sprite.y destX destY speed false (tweenOpts cc))))
(defAlias &ident FlxTween.linearMotion CHANGE_TO_MOVIE_LINEARMOTION)

(method linearMotionTo [:FlxSprite sprite :String positionKey :Float speed &opt :Void->Void cc :String soundLoop :Float volumeMod]
    (let [position (resolvePosition positionKey)]
        (linearMotion sprite position.x position.y speed cc soundLoop volumeMod)))

(method newFlxSet [name assetPath]
    (let [setSprite (new FlxSprite 0 0)]
        (setSprite.loadGraphic assetPath false 0 0 true) // Load uniquely so we can draw on sets for specific scenes
        (newSet name setSprite)))

(method newFlxSound [name path &opt :Float volume]
    (set volume (or volume 1))
    (assert (<= 0 volume 1))
    (let [s (FlxG.sound.load path)]
        (set s.volume volume)
        (set s.persist true)
        (newSound name s)))

(method newFlxSong [name path]
    (newSong name path))

(method newFlxVoiceTrack [name wavPath jsonPath]
    (newVoiceTrack name (FlxG.sound.load wavPath) (openfl.utils.Assets.getText jsonPath)))

(method newFlxVoiceTracks [:Array<String> names :Array<String> wavJsonPaths]
    (doFor name names
        (doFor [wavPath jsonPath] (groups wavJsonPaths 2 Throw)
            (newFlxVoiceTrack name wavPath jsonPath))))

(method newFlxProp [name path &opt :FlxSprite->Void prepareSprite]
    (let [propSprite (new FlxSprite 0 0)]
        (propSprite.loadGraphic path false 0 0 true) // Load props uniquely because they can be drawn on
        (when prepareSprite
            (prepareSprite propSprite))
        (newProp name propSprite)))

(defMacro flxSprite [asset &builder b]
    `(new FlxSprite 0 0 ,(b.field (symbolNameValue asset) (b.symbol "AssetPaths"))))

(method :Void update [:Float elapsed]
    (unless paused
        (doFor tween tweens
            (callPrivate tween "update" elapsed)
            (when tween.finished
                (callPrivate tween "finish"))))
    (.update (cast (director.shortcutHandler) kiss_tools.FlxKeyShortcutHandler<Dynamic>)))

(defAlias &ident flxDirector (cast director FlxDirector))

(prop &mut :FlxSprite _canvas null)
(method :FlxSprite canvas []
    (unless _canvas
        (set _canvas (new FlxSprite 0 0))
        (_canvas.makeGraphic FlxG.width FlxG.height FlxColor.BLACK))
    _canvas)
(method :StagePosition FullControl [:Int layer :RelativePosition rpos]
    (assert (<= 0 layer FlxDirector.LAYER_MAX) "Layer $layer is out of range 0-$FlxDirector.LAYER_MAX")
    (localVar pixel (new FlxSprite))
    (pixel.makeGraphic 1 1 FlxColor.BLACK)
    (let [[x y] (SpriteTools.positionOn pixel (canvas) rpos)]
        (new StagePosition x y layer)))

(method loopSound [name :Void->Void cc &opt :Float volumeMod]
    (playSound name (makeCC null) volumeMod false)
    (let [cs flxDirector.currentSounds
            sound (nth cs (- cs.length 1))
            onComplete sound.onComplete]
        (dictSet loopingOnCompletes sound onComplete)
        (set sound.onComplete null)
        (set sound.looped true))
    (cc))

(preload
    (unless uiCamera
        (set uiCamera (new flixel.FlxCamera))
        (set uiCamera.bgColor FlxColor.TRANSPARENT)
        (flixel.FlxG.cameras.add uiCamera)
        (set kiss_flixel.SimpleWindow.defaultCamera uiCamera))
    (unless screenCamera
        (set screenCamera (new flixel.FlxCamera))
        (set screenCamera.bgColor FlxColor.TRANSPARENT)
        (FlxG.cameras.add screenCamera))
    // These are set here so they're defined after FlxG.width and height have been set:
    (set STAGE_LEFT_X 150.0)
    (set STAGE_RIGHT_X (- FlxG.width STAGE_LEFT_X))
    (set ACTOR_Y (- FlxG.height 220.0))
    (set ACTOR_WIDTH 300)
    (set STAGE_BEHIND_DY 250.0)
    (set DIALOG_X ACTOR_WIDTH)
    (set DIALOG_Y (- FlxG.height 220.0))
    (set DIALOG_WIDTH (- FlxG.width ACTOR_WIDTH ACTOR_WIDTH))
    (set DIALOG_HEIGHT (- FlxG.height DIALOG_Y))
    #{
        stagePositions.put("Left", new StagePosition(
            STAGE_LEFT_X,
            ACTOR_Y,
            0.0
        ));
        stagePositions.put("Right", new StagePosition(
            STAGE_RIGHT_X,
            ACTOR_Y,
            0.0
        ));
        stagePositions.put("Left2", new StagePosition(
            STAGE_LEFT_X,
            ACTOR_Y,
            STAGE_BEHIND_DY
        ));
        stagePositions.put("Right2", new StagePosition(
            STAGE_RIGHT_X,
            ACTOR_Y,
            STAGE_BEHIND_DY
        ));
    }#
    (let [left (/ FlxG.width 6)
            right (- FlxG.width left)
            upper (/ FlxG.height 6)
            lower (- FlxG.height upper)
            centerX (/ FlxG.width 2)
            centerY (/ FlxG.height 2)
            layer 5]
        (stagePositions.put "ScreenCenter" (new StagePosition centerX centerY layer))
        (stagePositions.put "ScreenUpperLeft" (new StagePosition left upper layer))
        (stagePositions.put "ScreenUpperRight" (new StagePosition right upper layer))
        (stagePositions.put "ScreenLowerLeft" (new StagePosition left lower layer))
        (stagePositions.put "ScreenLowerRight" (new StagePosition right lower layer))
        (stagePositions.put "ScreenLowerCenter" (new StagePosition centerX lower layer))
        (stagePositions.put "ScreenUpperCenter" (new StagePosition centerX upper layer))))