(defMacroVar maxInt 1000000000)
(defMacroVar maxStringLength 20)
(defMacroVar maxExps 4)
(defMacroVar maxDepth 14)

(defAlias &ident nd (+ 1 macroDepth))

(defMacroFunction _macroList [func length]
    (for _ (range length) (func)))

(defMacro macroRepeat [exp length]
    `(begin ,@(_macroList ->{exp} (eval length))))

(defMacroFunction _randomLengthMacroList [func]
    (_macroList func (+ 2 (random (- maxExps 2)))))

(defMacroFunction _randomLetterString []
    (ReaderExp.StrExp
        (apply +
            (for _ (range (+ 1 (random (- maxStringLength 1))))
                (chooseRandom (.split "abcdefghijklmnopqrstuvwxyz" ""))))))

(defMacroFunction _randomInt []
    (symbol (Std.string (random maxInt))))

(defMacroFunction _randomFloat []
    (symbol (Std.string (+ (random maxInt) (/ 1 (random maxInt))))))

(defMacroFunction _randomFalsyExp [macroDepth]
    ((chooseRandom
        (concat
            [
                // null in conditionals is problematic in C#
                (#if !cs ->{`null} ->{`false})
                ->{`false}
                ->{`""}
                ->{`[]}
            ]
            (if (< macroDepth maxDepth)
                    [
                        ->{`(or ,@(_randomLengthMacroList ->(_randomFalsyExp nd)))}
                        ->{`(and
                                ,@(_randomLengthMacroList ->(_randomUncertainExp nd))
                                ,(_randomFalsyExp nd)
                                ,@(_randomLengthMacroList ->(_randomUncertainExp nd)))}
                    ]
                [])))))
(defMacro randomFalsyExp []
    (printExp (_randomFalsyExp 0) "Falsy"))

(defMacroFunction _randomTruthyExp [macroDepth]
    ((chooseRandom
        (concat
            [
                ->{`true}
                ->(_randomLetterString)
                ->(_randomInt)
                ->(_randomFloat)
            ]
            (if (< macroDepth maxDepth)
                    [
                        ->{`[,@(_randomLengthMacroList _randomLetterString)]}
                        ->{`[,@(_randomLengthMacroList _randomInt)]}
                        ->{`[,@(_randomLengthMacroList _randomFloat)]}
                        ->{`(and ,@(_randomLengthMacroList ->(_randomTruthyExp nd)))}
                        ->{`(or 
                                ,@(_randomLengthMacroList ->(_randomUncertainExp nd))
                                ,(_randomTruthyExp nd)
                                ,@(_randomLengthMacroList ->(_randomUncertainExp nd)))}
                    ]
                [])))))
(defMacro randomTruthyExp []
    (printExp (_randomTruthyExp 0) "Truthy"))

(defMacroFunction _randomUncertainExp [macroDepth]
    ((chooseRandom
        [
            ->(_randomFalsyExp macroDepth)
            ->(_randomTruthyExp macroDepth)
        ])))

(function _testTruthy []
    (macroRepeat (Assert.isTrue ?(randomTruthyExp)) 10))

(function _testFalsy []
    (macroRepeat (Assert.isFalse ?(randomFalsyExp)) 10))