(defun userConfigDir []
    (Path.join [
        (or (Sys.getEnv "MSYSHOME") (Sys.getEnv "HOME") (Sys.getEnv "UserProfile"))
        ".kiss"]))

(defun timeStamp []
    (.replace (.replace (.toString (Date.now)) ":" "-") " " "-"))

(defvar &mut activeConfigDir "")
(defvar &mut :KissConfig config null)

(defun :Void tryLoadConfig [&opt :String text]
    // TODO if a config object is active and a shortcut panel is open, dispose the panel before we lose the handle in the current config object
    (let [activeConfigPath (Path.join [activeConfigDir "config.js"])
            backupConfigPath (Path.join [activeConfigDir (+ "config" (timeStamp) ".js")])]
        // Backup existing config.js
        (when (FileSystem.exists activeConfigPath)
            (FileSystem.rename activeConfigPath backupConfigPath))
        // Supply the default (empty) config if the user doesn't have one
        (let [customConfigDir
                    (if (FileSystem.exists (userConfigDir))
                            (userConfigDir)
                        (Path.join [activeConfigDir "default"]))
                customConfigFiles
                    (FileSystem.readDirectory customConfigDir)]
            // Copy the custom config files to the active config directory
            (doFor file customConfigFiles
                (File.copy
                    (Path.join [customConfigDir file])
                    (Path.join [activeConfigDir file])))
            (let [buildResult
                        (ChildProcess.spawnSync "haxe" ["build.hxml"] (object cwd activeConfigDir))]
                (if (and !buildResult.error (= 0 buildResult.status))
                        // Successful compilation! require the config.js package.
                        // But since Node.require() caches modules by filename,
                        // copy it to a unique path first so hot-reloading works properly.
                        (let [activeConfigFile (Path.join [activeConfigDir "config.js"])
                                uniqueConfigFile (Path.join [activeConfigDir "$(.toShort (Uuid.v4)).js"])]
                            (File.copy activeConfigFile uniqueConfigFile)
                            (set config (the KissConfig .KissConfig (Node.require uniqueConfigFile)))
                            // (FileSystem.deleteFile uniqueConfigFile)
                            (config.registerBuiltins)
                            (config.registerCommand "Reload Kiss config" tryLoadConfig)
                            (config.prepareInterp)
                            // User-defined init:
                            (config.init)
                            (Vscode.window.showInformationMessage "Config loaded successfully!"))
                    // If there's a build error, restore the config.js backup
                    (begin
                        (when (FileSystem.exists backupConfigPath)
                            (FileSystem.rename backupConfigPath activeConfigPath))
                        (Vscode.window.showErrorMessage
                            (+ "Config failed to compile: "
                                (if buildResult.error 
                                        #| "" + buildResult.error|#
                                    #| "" + buildResult.stderr |#)))))))))

(defun _activate [:ExtensionContext context]
    (context.subscriptions.push
        (Vscode.commands.registerCommand
            "kiss.reloadConfig"
            tryLoadConfig))

    (context.subscriptions.push
        (Vscode.commands.registerCommand
            "kiss.runCommand"
            (lambda :Void [] 
                (if config
                        (.runCommand (the KissConfig config))
                    (Vscode.window.showErrorMessage "Can't run commands! No config is loaded.")))))

    (context.subscriptions.push
        (Vscode.commands.registerCommand
            "kiss.runLastCommand"
            (lambda :Void [] 
                (if config
                        (.runLastCommand (the KissConfig config))
                    (Vscode.window.showErrorMessage "Can't run commands! No config is loaded.")))))

    (context.subscriptions.push
        (Vscode.commands.registerCommand
            "kiss.runKeyboardShortcut"
            (lambda :Void [] 
                (if config
                        (.runKeyboardShortcut (the KissConfig config))
                    (Vscode.window.showErrorMessage "Can't run commands! No config is loaded.")))))

    // TODO overload Prelude.print to use showInformationMessage

    (set activeConfigDir (Path.join [context.extensionPath "config"]))
    (tryLoadConfig))
