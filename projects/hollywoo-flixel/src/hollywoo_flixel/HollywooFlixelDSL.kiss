(loadFrom "hollywoo" "src/hollywoo/HollywooDSL.kiss")
(loadFrom "hollywoo-flixel" "src/hollywoo_flixel/AssetMacros.kiss")

(defMacroVar subclass true)
(loadFrom "hollywoo" "src/hollywoo/Movie.kiss")

(method newFlxScene [name set time perspective]
    (let [setSprite (new FlxSprite 0 0)]
        (setSprite.loadGraphic (dictGet sets set))
        (newScene name (cast (new SceneFlxState setSprite time perspective)))))

// Destroy substates when the movie end is reached:
(cleanup
    (doFor =>name scene scenes
        (.destroy (cast scene SceneFlxState))))

(method newFlxSound [name path &opt :Float volume]
    (set volume (or volume 1))
    (assert (<= 0 volume 1))
    (let [s (FlxG.sound.load path)]
        (set s.volume volume)
        (set s.persist true)
        (newSound name s)))

(method newFlxVoiceTrack [name wavPath jsonPath]
    (newVoiceTrack name (FlxG.sound.load wavPath) (openfl.utils.Assets.getText jsonPath)))

(method newFlxProp [name path]
    (let [propSprite (new FlxSprite 0 0)]
        (propSprite.loadGraphic path)
        (newProp name propSprite)))

(method :Void update [:Float elapsed]
    (#when debug
        (when FlxG.keys.justPressed.N
            (doFor [idx label] (enumerate labels)
                (when (> label lastInstructionPointer)
                    (doFor =>labelName labelIdx labelsByName
                        (when (= labelIdx label) (trace "SKIPPING TO $labelName")))
                    (runInstruction (dictGet instructionPointersByLine label))
                    (break))))))