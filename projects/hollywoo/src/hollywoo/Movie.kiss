// Mostly immutable, reusable resources:
(prop :Map<String,Set> sets (new Map))
(prop :Map<String,Actor> actors (new Map))

// TODO for some reason this won't work when declared in Kiss syntax:
// Mutable representation of frames in time:
// var scenes:Map<String, Scene<Set, StagePosition, StageFacing, ScreenPosition, Actor>> = [];
(prop :Map<String,Bool> shownScenes (new Map))
(prop :Map<String,Bool> shownCharacters (new Map))
(function :Appearance appearanceFlag [:Map<String,Bool> map :String key]
    (if (dictGet map key)
            ReAppearance
        {
            (dictSet map key true)
            FirstAppearance
        }))

(prop &mut :String sceneKey "")
(method _currentScene [] (dictGet scenes sceneKey))

(defNew
    [
        // "View" in the Model-View-Controller architecture:
        &prop :Director<Set,StagePosition,StageFacing,ScreenPosition,Actor> director
    ]

    (super))

(method newSet [name :Set set :Continuation cc]
    (dictSet sets name set)
    (cc))

(method newSceneFromSet [name :String setKey :SceneTime time :ScenePerspective perspective :Continuation cc]
    (dictSet scenes name (objectWith
                                [
                                    set
                                        (dictGet sets setKey)
                                    characters
                                        (new Map)
                                ]
                                time
                                perspective))
    (cc))

(method newScene [name :Scene<Set,StagePosition,StageFacing,ScreenPosition,Actor> scene :Continuation cc]
    (dictSet scenes name scene)
    (cc))

(method setScene [name :Continuation cc]
    (set sceneKey name)
    (director.showScene
        (dictGet scenes name)
        (appearanceFlag shownScenes name)
        cc))

(method newActor [name :Actor actor :Continuation cc]
    (dictSet actors name actor)
    (cc))

(method addCharacter [actorName :StagePosition position :StageFacing facing :Continuation cc]
    (let [character (object stagePosition position stageFacing facing actor (dictGet actors actorName))]
        (dictSet .characters (_currentScene) actorName character)
        (director.showCharacter
            character
            (appearanceFlag shownCharacters actorName)
            cc)
        ))

/* TODO
(method removeCharacter [

])
*/

// TODO moveCharacter remove them, add them to another scene
// TODO moveCharacterAndFollow remove them, add them to another scene, set that the scene

(method normalSpeech [actorName wryly text cc]
    (director.showDialog (OnScreen (dictGet .characters (_currentScene) actorName)) wryly text cc))

(method onPhoneSpeech [actorName wryly text cc]
    (director.showDialog (ifLet [charOnScreen (dictGet .characters (_currentScene) actorName)]
                                    (OnScreen charOnScreen)
                                (FromPhone (dictGet actors actorName))) wryly text cc))